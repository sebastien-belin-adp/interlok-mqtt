plugins {
  id 'java'
  id 'maven'
  id 'maven-publish'
  id 'jacoco'
  id 'eclipse'
  // Manage optional dependencies in maven pom.
  id 'nebula.optional-base' version '5.0.3'
  id 'com.github.spotbugs' version '4.5.1'
  id 'org.owasp.dependencycheck' version '6.0.2'
  id "io.freefair.lombok" version "5.2.1"
}

ext {
  interlokCoreVersion = project.hasProperty('interlokCoreVersion') ? project.getProperty('interlokCoreVersion') : '3.11-SNAPSHOT'
  releaseVersion = project.hasProperty('releaseVersion') ? project.getProperty('releaseVersion') : '3.11-SNAPSHOT'
  nexusBaseUrl = project.hasProperty('nexusBaseUrl') ? project.getProperty('nexusBaseUrl') : 'https://nexus.adaptris.net/nexus'
  mavenPublishUrl = project.hasProperty('mavenPublishUrl') ? project.getProperty('mavenPublishUrl') : nexusBaseUrl + '/content/repositories/snapshots'
  javadocsBaseUrl = nexusBaseUrl + "/content/sites/javadocs/com/adaptris"
  repoUsername = project.hasProperty('repoUsername') ? project.getProperty('repoUsername') : 'unknown'
  repoPassword = project.hasProperty('repoPassword') ? project.getProperty('repoPassword') : 'unknown'
  defaultNexusRepo = project.hasProperty('defaultNexusRepo') ? project.getProperty('defaultNexusRepo') : 'https://repo1.maven.org/maven2/'
  offlineJavadocPackageDir = new File(project.buildDir, "offline-javadoc-packages")
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")

  interlokJavadocs= project.hasProperty('interlokJavadocs') ? project.getProperty('interlokJavadocs') : javadocsBaseUrl + "/interlok-core/" + interlokCoreVersion
  interlokCommonJavadocs= project.hasProperty('interlokCommonJavadocs') ? project.getProperty('interlokCommonJavadocs') : javadocsBaseUrl + "/interlok-common/" + interlokCoreVersion
  componentName = "Interlok MQTT"
  organizationName = "Adaptris Ltd"
  organizationUrl = "http://www.adaptris.com"
  slf4jVersion = "1.7.30"
  activeMqVersion = "5.16.0"
}

sourceCompatibility = 1.8
group   = "com.adaptris"
version = releaseVersion
def versionDir = "$buildDir/version"
generateLombokConfig.enabled = false

if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption("Xdoclint:none", "-quiet")
    }
  }
}
ext.gitBranchNameOrTimestamp = { branchName ->
  if (branchName.equals("HEAD") || branchName.equals("develop") || branchName.startsWith("release")) {
    return new Date().format('HH:mm:ss z');
  }
  return branchName;
}

ext.buildInfo = { ->
   new ByteArrayOutputStream().withStream { os ->
      exec {
        executable = "git"
        args = ["rev-parse", "--abbrev-ref", "HEAD"]
        standardOutput = os
      }
      def branchName = os.toString().replaceAll("\r", "").replaceAll("\n", "").trim();
      return gitBranchNameOrTimestamp(branchName);
    }
}

repositories {
  mavenCentral()
  maven { url "$defaultNexusRepo" }
  maven { url "$nexusBaseUrl/content/groups/public" }
  maven { url "$nexusBaseUrl/content/groups/interlok" }
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

configurations {
  javadoc {}
  offlineJavadocPackages {}
  all*.exclude group: 'c3p0'
  all*.exclude group: 'commons-logging'
  all*.exclude group: 'javamail'
  all*.exclude group: 'javax.mail', module: 'mail'
  all*.exclude group: 'org.glassfish.hk2.external'
  all*.exclude group: 'xalan', module: 'xalan'
  all*.exclude group: 'net.sf.saxon', module: 'saxon'
  all*.exclude group: 'org.codehaus.woodstox'
  all*.exclude group: 'org.eclipse.jetty.orbit', module: 'javax.mail.glassfish'
  all*.exclude group: 'com.fasterxml.woodstox'
  // INTERLOK-3197 exclude old javax.mail
  all*.exclude group: 'com.sun.mail', module: 'javax.mail'
  all*.exclude group: 'javax.validation', module: 'validation-api'
  all*.exclude group: 'javax.activation', module: 'activation'
  all*.exclude group: 'javax.activation', module: 'javax.activation-api'
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}

// In this section you declare the dependencies for your production and test code
dependencies {
  compile ("com.adaptris:interlok-core:$interlokCoreVersion") { changing= true}
  compile ("com.adaptris:interlok-common:$interlokCoreVersion") { changing= true}

  compile "org.slf4j:slf4j-api:$slf4jVersion"

  compile "javax.transaction:jta:1.1"
  compile "org.apache.httpcomponents:httpclient:4.5.13"
  compile "commons-codec:commons-codec:1.15"
  compile "org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5"

  annotationProcessor ("com.adaptris:interlok-core-apt:$interlokCoreVersion") { changing= true}

  // specified by interlok-common in its compile scope.
  // runtime "org.slf4j:slf4j-log4j12:$slf4jVersion"
  // runtime "org.slf4j:jcl-over-slf4j:$slf4jVersion"

  testCompile "junit:junit:4.13"
  testCompile "org.slf4j:slf4j-simple:$slf4jVersion"
  // exclude any log4j dependencies so that we can just use slf4j simple.
  // works around INTERLOk-3233
  testCompile "com.adaptris:interlok-stubs:$interlokCoreVersion", {
    changing= true
    exclude group: "org.apache.logging.log4j"
  }
  // testCompile "log4j:log4j:1.2.17"
  testCompile ("org.mockito:mockito-core:3.5.13")
  testCompile ("org.mockito:mockito-inline:3.5.13")
  testCompile "org.apache.activemq:activemq-broker:$activeMqVersion"
  testCompile "org.apache.activemq:activemq-mqtt:$activeMqVersion"

  javadoc("com.adaptris:interlok-core-apt:$interlokCoreVersion") { changing= true}
  offlineJavadocPackages ("com.adaptris:interlok-core:$interlokCoreVersion:javadoc@jar") { changing= true}
  offlineJavadocPackages ("com.adaptris:interlok-common:$interlokCoreVersion:javadoc@jar") { changing= true}

}

jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
               "Build-Jdk": System.getProperty('java.version'),
               "Implementation-Title": componentName,
               "Implementation-Version": project.version,
               "Implementation-Vendor-Id": project.group,
               "Implementation-Vendor": organizationName,
               "Implementation-URL": organizationUrl)
  }
}

javadoc {
  configure(options) {
    options.linksOffline(interlokJavadocs, offlineJavadocPackageDir.getCanonicalPath() + "/interlok-core-$interlokCoreVersion-javadoc.jar")
    options.linksOffline(interlokCommonJavadocs, offlineJavadocPackageDir.getCanonicalPath() + "/interlok-common-$interlokCoreVersion-javadoc.jar")
    taglets = ["com.adaptris.taglet.ConfigTaglet", "com.adaptris.taglet.LicenseTaglet"]
    options.addStringOption "tagletpath", configurations.javadoc.asPath
  }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = "javadoc"
  from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
  classifier = "examples"
  from new File(buildDir, "/examples")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
  archives javadocJar
  archives examplesJar
  archives sourcesJar
}

sourceSets {
  main {
    output.dir(versionDir, builtBy: "generateVersion")
  }
}

  // Generate the META-INF/adaptris-version file
task generateVersion {
  doLast {
    def versionFile = new File(new File(versionDir, "META-INF"), "adaptris-version")
    versionFile.getParentFile().mkdirs()
    ant.propertyfile(file: versionFile) {
      entry(key: "component.name", value: componentName)
      entry(key: "build.version", value: releaseVersion)
      entry(key: 'groupId', value: project.group)
      entry(key: 'artifactId', value: project.name)
      entry(key: "build.date", value: new Date().format("yyyy-MM-dd"))
      entry(key: 'build.info', value: buildInfo())
    }
  }
}

task offlinePackageList(type: Copy) {
  description 'Build an offline package-list for javadocs'
  group 'Documentation'

  project.configurations.offlineJavadocPackages.collect { dependency ->
    from(zipTree(dependency)) {
      into dependency.name
    }
  }
  include "package-list"
  into offlineJavadocPackageDir
}


publishing {
  publications {
    mavenCustom(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact examplesJar { classifier "examples" }
      artifact sourcesJar { classifier "sources" }

      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", "Connect to a MQTT broker. Consume and produce MQTT messages.")
        // asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/mqtt-integration.html")
        // asNode().appendNode("logo", "/interlok/img/mqtt.png")
        def properties = asNode().appendNode("properties")
        properties.appendNode("externalUrl", "http://mqtt.org")
        properties.appendNode("notes", "Requires additional jars not automatically delivered")
        properties.appendNode("license", "false")
        properties.appendNode("target", "3.5.0+")
        properties.appendNode("tags", "MQTT")
        properties.appendNode("repository", "https://github.com/adaptris/interlok-mqtt")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}


delombok {
  target = delombokTargetDir
}

task lgtmCompile(type: JavaCompile, dependsOn: delombok) {
  group 'Build'
  description 'Compile for lgtm'

  source = sourceSets.main.extensions.delombokTask
  destinationDir = sourceSets.main.java.outputDir
  classpath = project.sourceSets.main.compileClasspath
}

task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}


spotbugsMain {
  effort = "max"
  reportLevel = "high"
  // showProgress = "true"
  effort = "max"
  // Ignore failures?
  // ignoreFailures = false
  reports {
    xml {
      enabled = false
    }
    html {
      enabled = true
    }
  }
  // includeFilter = new File("$rootDir/gradle/spotbugs-filter.xml")
  // excludeFilter = new File("$rootDir/gradle/spotbugs-exclude.xml")
}
dependencyCheck  {
  suppressionFiles= [ "https://raw.githubusercontent.com/adaptris/interlok/develop/gradle/owasp-exclude.xml", "$rootDir/gradle/owasp-exclude.xml" ]
  skipConfigurations = [ "antSql", "spotbugs", "umlDoclet", "offlineJavadocPackages", "javadoc", "jacocoAnt", "jacocoAgent", "spotbugsPlugins", "spotbugsSlf4j" ]
  formats = [ "HTML", "JUNIT" ]
  junitFailOnCVSS = 7.0
  failBuildOnCVSS = 7.0
  analyzers {
    assemblyEnabled=false
  }
}

// disable spotbugsTests which checks our test code..
spotbugsTest.enabled = false
clean.dependsOn deleteGeneratedFiles
check.dependsOn jacocoTestReport
javadoc.dependsOn offlinePackageList
